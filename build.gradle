plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.2.51'
    id "org.jetbrains.kotlin.kapt" version "1.2.51"
    id "net.ltgt.apt" version "0.18"
    id 'net.nemerosa.versioning' version '2.6.1'
    id 'com.google.cloud.tools.jib' version '0.9.8'
}

group 'com.austinv11.stats'
version '1.0-SNAPSHOT'

sourceCompatibility = 10

repositories {
    jcenter()
    maven {
        url "https://repo.spring.io/milestone"
    }
    maven {
        url  "https://jitpack.io"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile 'org.jetbrains.kotlin:kotlin-reflect'
    compile "com.github.austinv11:Commandable:5f2ae5f"
    compile 'ch.qos.logback:logback-classic:1.3.0-alpha4'

    kapt "com.github.austinv11:Commandable:5f2ae5f"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.freeCompilerArgs = ["-Xjsr305=strict"]
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.austinv11.stats.MainKt',
                'Built-By': System.properties['user.name'],
                'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Build-Revision': versioning.info.commit,
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

jib {
    from {
        image = 'openjdk:10-jre'
    }
    to {
        if (project.hasProperty("DockerTag")) {
            image = "austinv11/statsbot:" + project.findProperty("DockerTag")
        } else {
            image = "austinv11/statsbot:$version"
        }
        auth {
            username = 'austinv11'
            password = project.findProperty("DockerPassword")
        }
    }
    container {
        jvmFlags = ['-Xms256m', '-Xmx512m']
        mainClass = 'com.austinv11.stats.BotKt'
        args = ["\$BOT_TOKEN"]
    }
}